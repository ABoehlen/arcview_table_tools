'#################################################################
'#
'# Filename:     tabletools_calculate_table.ave
'# Author:       Adrian Boehlen
'# Date:         18.05.2014
'# Version:      0.7
'#
'# Purpose:      ermoeglicht Berechnungen in einer Tabelle.
'#               Dazu muss die Expression in der zweiten Zeile
'#               einer Spalte des Typs "String" stehen
'#               Werden benutzerdefinierte Variablen benoetigt,
'#               sind diese in der ersten Zeile zu initialisieren.
'#
'#               Sind Zeilen selektiert, wird die Berechnung
'#               nur in den selektierten Zeilen durchgefuehrt,
'#               ansonsten in allen
'#
'#               Die erste und zweite Zeile werden durch
'#               die Berechnung nicht beeinflusst
'#
'#################################################################

'# die Tabelle muss im Edit-Modus sein

_theTable = av.GetActiveDoc
_theVTab = _theTable.GetVTab
_theFields = _theVTab.GetFields

if (_theVTab.CanEdit.Not) then
  MsgBox.Info("Die Tabelle kann nicht bearbeitet werden","Abbruch")
  return nil
end

if (_theVTab.IsEditable.Not) then
  _theVTab.StartEditingWithRecovery
  theEditstatus = 0
else
  theEditstatus = 1
end

'# Es muss ein Feld aktiv sein

if (_theTable.GetActiveField = nil) then
  MsgBox.Info("Es ist kein Feld aktiv","Abbruch")
  return nil
end

'# diverse Variablen initialisieren

_theField = _theTable.GetActiveField
_theFieldName = _theField.GetName
_r = 0  '# Standardvariable, die den kalkulierten Wert aufnimmt

'################################################################################
'# globale Variablen in der ersten Zeile lesen und in "theVariables" speichern
'# Expression in der zweiten Zeile lesen und in "_theExpression" speichern
'# bei einer numerischen Expression muss der Wert mit 100000 multipliziert werden

for each rec in _theVTab
  if (rec = 0) then
    theVariables = _theVTab.ReturnValue(_theField, rec)
  end
  if (rec = 1) then
    if (_theVTab.ReturnValue(_theField, rec).AsString.Contains("AsString")) then
      numberExpr = 0
      _theExpression = _theVTab.ReturnValue(_theField, rec)
    else
      numberExpr = 1
      _theRec = rec    '# damit als globale Variable definiert
      _theExpression = _theVTab.ReturnValue(_theField, _theRec)
      _theExpression = _theExpression + 10.AsChar + "if (_r <> 0) then"
      _theExpression = _theExpression + 10.AsChar + "  _r = _r * 100000"
      _theExpression = _theExpression + 10.AsChar + "end"
      _theExpression = _theExpression + 10.AsChar + "_theVTab.SetValue(_theField,_theRec,_r.AsString)"
    end
  end
end

'# globale Variablen aus erster Zeile initialisieren, wenn solche vorhanden sind
'# Expression aus zweiter Zeile auf uebrige Zeilen anwenden

theSelected = _theVTab.GetSelection

if (_theVTab.GetSelection.Count = 0) then
  wasSelected = 0
  for each rec in _theVTab
    if (rec = 0) then
      if (theVariables.AsString.Contains("_")) then
        '# temporaeres Script aufbauen, ausfuehren, und wieder loeschen
        theSEd = SEd.MakeWithGUI(Av.GetProject.GetSelectedGUI.GetName)
        theSEd.SetSource(theVariables.AsString)
        theSEd.Compile
        if (theSEd.IsCompiled.Not) then
          MsgBox.Error("fehlerhafte Anweisung","Abbruch")
          Av.GetProject.RemoveDoc(theSEd)
          return nil
        end
        Av.Run(theSEd.AsString,"")     
        Av.GetProject.RemoveDoc(theSEd)
      else
        continue
      end   
    elseif (rec = 1) then
      continue        '# Zeile mit Expression ueberspringen
    else  
      _theRec = rec   '# damit als globale Variable im temporaeren Script verfuegbar
      theSelected.Set(rec)
      _theVTab.SetSelection(theSelected)
      _theVTab.UpdateSelection
      
      '# Inhalt der uebrigen Felder als Variable uebernehmen
      
      _theFieldContent = ""
      for each theActualField in _theFields
        _theFieldContent = _theFieldContent + "_" + theActualField.GetName.AsString + " = "
        if (_theVTab.ReturnValue(theActualField, rec ).AsString = "") then
          _theFieldContent = _theFieldContent + "0" + 10.AsChar
        else
          _theFieldContent = _theFieldContent + _theVTab.ReturnValue(theActualField, rec ).AsString + 10.AsChar
        end
      end
      _theSource = _theFieldContent + _theExpression
      
      '# temporaeres Script aufbauen, ausfuehren, und wieder loeschen
      
      theSEd = SEd.MakeWithGUI(Av.GetProject.GetSelectedGUI.GetName)
      theSEd.SetSource(_theSource.AsString)
      theSEd.Compile
      if (theSEd.IsCompiled.Not) then
        MsgBox.Error("fehlerhafte Anweisung","Abbruch")
        Av.GetProject.RemoveDoc(theSEd)
        return nil
      end
      Av.Run(theSEd.AsString,"")     
      Av.GetProject.RemoveDoc(theSEd)      
    end
  end
  
  '# bei einer numerischen Expression muss der Wert durch 100000 geteilt werden
  if (numberExpr = 1) then
    for each rec in theSelected
      theValue = _theVTab.ReturnValue(_theField, rec )
      theValue = theValue.AsNumber / 100000
      _theVTab.SetValue(_theField,rec,theValue.AsString)
    end
  end
else
  wasSelected = 1
  for each rec in _theVTab
    if (rec = 0) then
      continue
    else   
      _theVTab.Calculate(_theExpression,_theField)
    end
  end
  
  '# wenn eine numerische Expression muss der Wert durch 100000 geteilt werden
  if (numberExpr = 1) then
    for each rec in theSelected
      theValue = _theVTab.ReturnValue(_theField, rec )
      theValue = theValue.AsNumber / 100000
      _theVTab.SetValue(_theField,rec,theValue.AsString)
    end
  end       
end

'if (not (_theVTab.Calculate(_theExpression,_theField))) then
'  MsgBox.Warning("Syntax Error", "Expression")
'end

'# Anzeige aktualisieren

_theVTab.UpdateSelection

'# Selektion aufheben, wenn zuvor nichts selektiert

if (wasSelected = 0) then
  theSelected.ClearAll
  _theVTab.UpdateSelection
end

'# Editsession beenden, wenn zu Beginn nicht gestartet

if (theEditstatus = 0) then
  _theVTab.SetEditable(false)
end

'# alle globalen Variablen loeschen
Av.ClearGlobals
